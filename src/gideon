#!/usr/bin/env guile
!#

;; Not defining a module here, as this is the CLI interface. 

(use-modules (ice-9 match)
	     (ice-9 getopt-long)
	     (srfi srfi-1)
	     (gideon-lib cmds)
	     (gideon-lib system)
	     (gideon-lib term))

(define option-spec
  '((manifest (single-char #\m) (value #t))
    (dry-run (single-char #\d) (value #f))))

(define options (getopt-long (command-line) option-spec))

(define provided-manifest (option-ref options 'manifest #f))

(define cmdline (command-line))

(set! cmdline 
      (remove (lambda (str)
		(or (string-contains str "--manifest")
		    (string= str "-m")
		    (string-contains str "--dry-run")
		    (string= str "-d"))) cmdline))

(set! cmdline (delete provided-manifest cmdline))

(parameterize ((%dry-run? (option-ref options 'dry-run #f)))
  (match cmdline
    ((_ "add" manifest-path ...) (gideon-add manifest-path))
    ((_ "del" manifest-path ...) (gideon-del manifest-path))
    ((_ "install" pkgs ...)	 (gideon-install pkgs provided-manifest))
    ((_  "remove" pkgs ...)	 (gideon-remove pkgs provided-manifest))
    ((_ "apply")		 (gideon-apply))
    ((_ "list")			 (gideon-list))
    ((_ "rescan")                (gideon-rescan))
    ((_ "purge")                 (gideon-purge))
    ((_ "init")			 (gideon-init))
    (_ (usage))))
